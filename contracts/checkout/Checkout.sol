pragma solidity ^0.4.17;

import "../core/DINRegistry.sol";
import "../core/Orders.sol";

/** @title Checkout with Ether. */
contract Checkout {
    DINRegistry public registry;
    Orders public orders;


    // Prevent Solidity "stack too deep" error.
    struct Order {
        uint256 DIN;
        uint256 quantity;
        uint256 totalPrice;
        uint256 priceValidUntil;
        address merchant;
        address owner;
    }

    // Log Solidity errors
    event LogError(string error);

    /** @dev Constructor.
      * @param _registry The DIN Registry contract address.
      * @param _orders The Orders contract address.
      */
    function Checkout(DINRegistry _registry, Orders _orders) public {
        registry = _registry;
        orders = _orders;
    }

    /** @dev Buy a product.
      * param orderValues:
        [0] DIN The Decentralized Identification Number (DIN) of the product to buy.
        [1] quantity The quantity to buy.
        [2] totalPrice Total price of the purchase, in wei.
        [3] priceValidUntil Expiration time (Unix timestamp).
      * @param merchant The merchant address.
      * @param nonceHash The hash of a nonce generated by a client. The nonce can be used as a proof of purchase.
      * @param v ECDSA signature parameter v.
      * @param r ECDSA signature parameter r.
      * @param s ECDSA signature parameter s.
      * @return orderID A unique ID for the order.
      */
    function buy(
        uint256[4] orderValues,
        address merchant,
        bytes32 nonceHash,
        uint8 v,
        bytes32 r,
        bytes32 s
    )
        payable
        public
        returns (uint256)
    {       
         Order memory order = Order({
            DIN: orderValues[0],
            quantity: orderValues[1],
            totalPrice: orderValues[2],
            priceValidUntil: orderValues[3],
            merchant: merchant,
            owner: registry.owner(orderValues[0]) // Get the DIN owner address from the DIN registry.
        });

        if (isValidOrder(order, v, r, s) == false) {
            // Return Ether to buyer.
            msg.sender.transfer(msg.value);
            return 0;
        }

        // Transfer Ether to the merchant.
        merchant.transfer(msg.value);

        // Create a new order
        uint256 orderID = orders.createOrder(
            nonceHash,
            msg.sender, // Buyer
            merchant,
            order.DIN,
            order.quantity,
            order.totalPrice
        );

        // Return the unique order ID.
        return orderID;
    }

    /**
      * @dev Verify that an order is valid.
      * @return valid Validity of the order.
      */
    function isValidOrder(
        Order order,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) 
        internal 
        returns (bool) 
    {
        if (block.timestamp > order.priceValidUntil) {
            LogError("Offer expired");
            return false;
        }

        if (order.merchant == address(0x0)) {
            LogError("Invalid merchant");
            return false;
        }

        if (msg.value != order.totalPrice) {
            LogError("Invalid price");
            return false;
        }

        uint256 unitPrice = order.totalPrice / order.quantity;

        // Calculate the hash of the parameters provided by the buyer.
        bytes32 hash = keccak256(order.DIN, unitPrice, order.priceValidUntil, order.merchant);

        // Verify that the DIN owner has signed the provided inputs.
        if (isValidSignature(order.owner, hash, v, r, s) == false) {
            LogError("Invalid signature");
            return false;
        }

        return true;
    }

    /**
      * @dev Verify that an order signature is valid.
      * @param signer address of signer.
      * @param hash Signed Keccak-256 hash.
      * @param v ECDSA signature parameter v.
      * @param r ECDSA signature parameters r.
      * @param s ECDSA signature parameters s.
      * @return valid Validity of the order signature.
      */
    function isValidSignature(
        address signer,
        bytes32 hash,
        uint8 v,
        bytes32 r,
        bytes32 s
    )
        public
        pure
        returns (bool valid)
    {
        return signer == ecrecover(
            keccak256("\x19Ethereum Signed Message:\n32", hash),
            v,
            r,
            s
        );
    }

}